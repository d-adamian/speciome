plugins {
    id 'java'
    id 'checkstyle'
    id 'jacoco'
    id 'info.solidsoft.pitest' version '1.5.1'
}

repositories {
    mavenCentral()
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

dependencies {
    implementation('com.google.guava:guava:30.1-jre')

    testImplementation("org.junit.jupiter:junit-jupiter-api:5.7.1")
    testImplementation("org.junit.jupiter:junit-jupiter-params:5.7.1")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.7.1")
    testImplementation("org.assertj:assertj-core:3.19.0")
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

checkstyleMain {
    source = 'src/main/java'
}

checkstyleTest {
    source = 'src/main/test'
}

pitest {
    targetClasses = ['com.epam.specimenbase.*']
    excludedClasses = [
            'com.epam.specimenbase.catalog.domain.Passwords' // salting password hashes not covered
    ]
    junit5PluginVersion = '0.12'
    outputFormats = ['HTML']
    mutationThreshold = 100
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report

    reports {
        xml.enabled false
        csv.enabled true
        html.enabled true
    }
}


build.dependsOn 'pitest'


