plugins {
    id 'application'
    id 'org.springframework.boot' version '2.5.0'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springdoc:springdoc-openapi-ui:1.5.10'
    implementation 'org.springdoc:springdoc-openapi-security:1.5.10'

    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'org.flywaydb:flyway-gradle-plugin:8.4.1'

    implementation project(':speciome-catalog-core')
    implementation project(':speciome-catalog-persistence-api')
    implementation project(':speciome-catalog-persistence-spring-data-jpa')
    implementation project(':speciome-reactjs')

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
}

task copyWebApp(type: Copy) {
    dependsOn ":speciome-reactjs:build"
    from "$rootDir/speciome-reactjs/build"
    into "$rootDir/speciome-catalog-service-spring-mvc/build/resources/main/static"
}

processResources.dependsOn(copyWebApp)

test {
    useJUnitPlatform()
}

// For Heroku builds
task copyToLib(type: Copy) {
    from "$buildDir/libs"
    into "$rootProject.buildDir/libs"
}
copyToLib.dependsOn(bootJar)
stage.dependsOn(copyToLib)
